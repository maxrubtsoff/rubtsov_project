#Область ПрограммныйИнтерфейс

// Покупатель - Структура:
//  * ТекущаяПланета - СправочникСсылка.Планеты - Планета, на которой находится корабль.
//  * ДенегВНаличии - Число - Денег у покупателя.
//  * СвободноВТрюме - Число - сколько масимально единиц объема способен вместить корабль.
//  * ВместимостьТрюма - Число - сколько масимально единиц объема способен вместить корабль.
//  * ТрюмКорабля - ТаблицаЗначений:
//    * Товар - СправочникСсылка.Товар - Товар, который есть в трюме корабля
//    * Объем - Число - Количество единиц объема в трюме
//    * Себестоимость - Число - Себестоимость товара из трюма
//
Процедура ПриОпределенииПокупателя(Покупатель) Экспорт
	
	Покупатель.ТекущаяПланета = Константы.ТекущаяПланета.Получить();
	Покупатель.ДенегВНаличии = РегистрыНакопления.Касса.Остатки()[0].Сумма;
	Покупатель.СвободноВТрюме = РегистрыНакопления.ЗаполненностьТрюма.Остатки()[0].Объем;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаполненностьТрюма.Объем КАК Объем
		|ИЗ
		|	РегистрНакопления.ЗаполненностьТрюма КАК ЗаполненностьТрюма
		|ГДЕ
		|	ЗаполненностьТрюма.Регистратор ССЫЛКА Документ.УвеличениеОбъемаТрюма";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Покупатель.ВместимостьТрюма = ВыборкаДетальныеЗаписи.Объем;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТрюмОстатки.Товар КАК Товар,
		|	ТрюмОстатки.ОбъемОстаток КАК Объем
		|ИЗ
		|	РегистрНакопления.Трюм.Остатки КАК ТрюмОстатки";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НовСтр = Покупатель.ТрюмКорабля.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
КонецПроцедуры

// Планета - СправочникСсылка.Планеты - Планета на которой совершалась сделка
// Товар - СправочникСсылка.Товары - Товар, который покупается
// Количество - Число - Объем купленного товара
// ЦенаПокупка - Число
//
Процедура ПокупкаТовара(Панета, Товар, Количество, ЦенаПокупка) Экспорт
	
	Покупка = Документы.Покупка.СоздатьДокумент();
	Покупка.Дата = ТекущаяДатаСеанса();
	Покупка.Товар = Товар;
	Покупка.Количество = Количество;
	Покупка.Цена = ЦенаПокупка;
	Покупка.Объем = РегистрыСведений.ОбъемыТоваров.Получить(Новый Структура("Товар", Товар)).Объем * Количество;
	Покупка.Записать(РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры

// Планета - СправочникСсылка.Планеты - Планета на которой совершалась сделка
// Товар - СправочникСсылка.Товары - Товар, который продается
// Количество - Число - Объем продаваемого товара
// ЦенаПродажи - Число
//
Процедура ПродажаТовара(Панета, Товар, Количество, ЦенаПродажи) Экспорт
	
	Продажа = Документы.Продажа.СоздатьДокумент();
	Продажа.Дата = ТекущаяДатаСеанса();
	Продажа.Товар = Товар;
	Продажа.Количество = Количество;
	Продажа.Цена = ЦенаПродажи;
	Продажа.Объем = РегистрыСведений.ОбъемыТоваров.Получить(Новый Структура("Товар", Товар)).Объем * Количество;
	Продажа.Записать(РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры

#КонецОбласти
