#Область СлужебныйПрограммныйИнтерфейс

Функция СтоимостиПеремещенийПоНаправлениям(Планета) Экспорт 
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("ПланетаНазначения");
	Результат.Колонки.Добавить("ТребуетсяТоплива");
	
	Если Планета.Пустая() Тогда
		Возврат Результат;
	КонецЕсли;
	
	ИмяПланеты = Планета.ИмяПредопределенныхДанных;
	
	Макет = ПолучитьМакет("СтоимостьПеремещения");
	
	КоличествоСтрок = Макет.ВысотаТаблицы;
	КоличествоКолонок = Макет.ШиринаТаблицы;
	
	Для ИндексКолонки = 1 По КоличествоКолонок Цикл
		
		НазваниеКолонки = Макет.Область(1, ИндексКолонки).Текст;
		Если НазваниеКолонки = ИмяПланеты Тогда
			
			Для ИндексСтроки = 2 По КоличествоСтрок Цикл
				
				Тариф = Макет.Область(ИндексСтроки, ИндексКолонки).Текст;
				Если Тариф <> "-" Тогда
					
					ИмяПланетыНазначения = Макет.Область(ИндексСтроки, 0).Текст;
					
					ПланетаНазначения = Справочники.Планеты[ИмяПланетыНазначения];
					
					ОписаниеТипаЧисло = Новый ОписаниеТипов("Число");
					ТребуетсяТоплива = ОписаниеТипаЧисло.ПривестиЗначение(Тариф);
					
					СтрокаРезультата = Результат.Добавить();
					СтрокаРезультата.ПланетаНазначения = ПланетаНазначения;
					СтрокаРезультата.ТребуетсяТоплива = ТребуетсяТоплива;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
