#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьКарту();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Полетели(Команда)
	
	ТекущаяСтрока = Элементы.ВозможныеНаправления.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПолетелиНаСервере(ТекущаяСтрока);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПолетелиНаСервере(ИдентификаторСтрокиНаправления)
	
	ТекущаяСтрока = Объект.ВозможныеНаправления.НайтиПоИдентификатору(ИдентификаторСтрокиНаправления);
	
	Если ТекущаяСтрока.ТребуетсяТоплива > Объект.ОстатокТоплива Тогда
		ВызватьИсключение НСтр("ru = 'Топлива не достаточно!'");
	КонецЕсли;
	
	Местоположение = Новый Структура;
	Местоположение.Вставить("ТекущаяПланета", Объект.ТекущаяПланета);
	Местоположение.Вставить("ПанетаНазначения", ТекущаяСтрока.ПланетаНазначения);
	Местоположение.Вставить("ПотраченоТоплива", ТекущаяСтрока.ТребуетсяТоплива);
	КартаЗвездногоНебаПереопределяемый.ПриУстановкеМестоположения(Местоположение);
	
	ОбновитьКарту();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКарту()
	
	Местоположение = Новый Структура;
	Местоположение.Вставить("ТекущаяПланета", Справочники.Планеты.ПустаяСсылка());
	Местоположение.Вставить("ТекущийЗапасТоплива", 0);
	КартаЗвездногоНебаПереопределяемый.ПриОпределенииМестоположения(Местоположение);
	Объект.ТекущаяПланета = Местоположение.ТекущаяПланета;
	Объект.ОстатокТоплива = Местоположение.ТекущийЗапасТоплива;
	
	СтоимостиПеремещений = Обработки.КартаЗвездногоНеба.СтоимостиПеремещенийПоНаправлениям(Объект.ТекущаяПланета);
	Объект.ВозможныеНаправления.Загрузить(СтоимостиПеремещений);
	
КонецПроцедуры

#КонецОбласти
