#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьКонсоль();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПланетаПрейскурантаПриИзменении(Элемент)
	
	ОбновитьКонсоль();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Купить(Команда)
	
	ТекущаяСтрока = Элементы.Магазин.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КупитьНаСервере(ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура Продать(Команда)
	
	ТекущаяСтрока = Элементы.ТрюмКорабля.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПродатьНаСервере(ТекущаяСтрока);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура КупитьНаСервере(ИдентификаторСтрокиМагазина)
	
	ТекущаяСтрока = Объект.Магазин.НайтиПоИдентификатору(ИдентификаторСтрокиМагазина);
	
	Если Объект.Количество = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Выберите количество!'");
	КонецЕсли;
	
	Если Объект.Количество > Объект.СвободноВТрюме Тогда
		ВызватьИсключение НСтр("ru = 'Вместимости не достаточно!'");
	КонецЕсли;
	
	Если ТекущаяСтрока.ЦенаПокупки * Объект.Количество > Объект.ДенегВНаличии Тогда
		ВызватьИсключение НСтр("ru = 'Денег не достаточно!'");
	КонецЕсли;
	
	Панета = Объект.ТекущаяПланета;
	Товар = ТекущаяСтрока.Товар;
	Количество = Объект.Количество;
	ЦенаПокупка = ТекущаяСтрока.ЦенаПокупки;
	ТорговаяКонсольПереопределяемый.ПокупкаТовара(Панета, Товар, Количество, ЦенаПокупка);
	
	ОбновитьКонсоль();
	
КонецПроцедуры

&НаСервере
Процедура ПродатьНаСервере(ИдентификаторСтрокиТрюма)
	
	ТекущаяСтрока = Объект.ТрюмКорабля.НайтиПоИдентификатору(ИдентификаторСтрокиТрюма);
	
	Если Объект.Количество = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Выберите количество!'");
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Товар", ТекущаяСтрока.Товар);
	СтрокиМагазина = Объект.Магазин.НайтиСтроки(Отбор);
	
	Панета = Объект.ТекущаяПланета;
	Товар = ТекущаяСтрока.Товар;
	Количество = Объект.Количество;
	ЦенаПокупка = СтрокиМагазина[0].ЦенаПродажи;
	ТорговаяКонсольПереопределяемый.ПродажаТовара(Панета, Товар, Количество, ЦенаПокупка);
	
	ОбновитьКонсоль();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКонсоль()
	
	Покупатель = Новый Структура;
	Покупатель.Вставить("ТекущаяПланета", Справочники.Планеты.ПустаяСсылка());
	Покупатель.Вставить("ДенегВНаличии", 0);
	Покупатель.Вставить("СвободноВТрюме", 0);
	Покупатель.Вставить("ВместимостьТрюма", 0);
	Покупатель.Вставить("ТрюмКорабля", Новый ТаблицаЗначений);
	Покупатель.ТрюмКорабля.Колонки.Добавить("Товар");
	Покупатель.ТрюмКорабля.Колонки.Добавить("Объем");
	Покупатель.ТрюмКорабля.Колонки.Добавить("Себестоимость");
	
	ТорговаяКонсольПереопределяемый.ПриОпределенииПокупателя(Покупатель);
	Объект.ТекущаяПланета = Покупатель.ТекущаяПланета;
	Объект.ДенегВНаличии = Покупатель.ДенегВНаличии;
	Объект.СвободноВТрюме = Покупатель.СвободноВТрюме;
	Объект.ВместимостьТрюма = Покупатель.ВместимостьТрюма;
	Объект.ТрюмКорабля.Загрузить(Покупатель.ТрюмКорабля);
	
	Если ЗначениеЗаполнено(Объект.ПланетаПрейскуранта) Тогда
		Планета = Объект.ПланетаПрейскуранта;
		Элементы.ПанельТоргови.Доступность = Ложь;
		Элементы.Блокировка.Видимость = Истина;
	Иначе
		Планета = Объект.ТекущаяПланета;
		Элементы.ПанельТоргови.Доступность = Истина;
		Элементы.Блокировка.Видимость = Ложь;
	КонецЕсли;
	
	СтоимостиТоваровНаПланете = Обработки.ТорговаяКонсоль.СтоимостиТоваровНаПланете(Планета);
	Объект.Магазин.Загрузить(СтоимостиТоваровНаПланете);
	
КонецПроцедуры

#КонецОбласти
